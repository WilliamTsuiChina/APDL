!2017年10月26日
!塔身建模分析宏
!标准节类型：1-四面人字腹杆 2-四面K形腹杆 3-人字+顺斜腹杆
![内爬工况][含外挂架整体]
![自动提取结果][360工况求解][link]
!Z+为向上方向
!
TTYPE=3  !标准节类型: 1 2 3
FLAG=0   !循环计次变量

*DO,JJ,0,359,1   !循环工况求解开始
FLAG=FLAG+1
PARSAV  !清空前保存标量参数
/CLEAR
PARRES  !清空后释放标量参数
/PREP7
*AFUN,DEG
!所有输入参数定义
!定义立柱截面，H型钢
w1=300
w2=305
t1=15   !!!!!可变参数
t2=15   !!!!!可变参数
!定义腹杆截面，方管
b1=125   
t3=12   !!!!!可变参数
!定义顶部十字梁，矩形
b2=300   !!!!!可变参数
!定义塔身参数
L1=2400   !!!!!标准节中心距
H1=2000  !标准节每档高度
N1=14 !!!!!标准节数量
N2=N1*2  !标准节档数
N3=7    !!!!!夹持位置，从下往上第几档上端
!定义载荷
FFZ=100     !!!!!顶部竖向力，单位：吨
         
FFH=10      !!!!!上部水平力，单位：吨

MMT=700   !!!!!顶部弯矩，单位：吨.米

!外挂架参数
G1=4400 !!!外挂架主梁中心距
G2=2162 !!!外挂架一侧斜撑距离
G3=2162 !!!外挂架另一侧斜撑距离
G4=3590 !!!标准节中心到墙的距离
G5=8400 !!!外挂架竖向高度
!外挂架主梁截面参数
GW1=400  !宽
GH1=800  !高
GT1=40   !上板厚
GT2=20   !侧板厚
!外挂架竖向撑杆截面
GD1=377  !圆管直径
GT3=14   !壁厚
!外挂架水平撑杆截面
GD2=245  !圆管直径
GT4=10   !壁厚
!外挂架支撑框截面
GW2=320  !宽
GH2=400  !高
GT5=40   !上下
GT6=30   !中

!LINK10单元参数
LLINK=100
ALINK=31400
!上层挂架6个节点的反力数组：节点号，FX,FY,FZ
*DIM,UPSOL,ARRAY,6,4
UPFX=0
UPFY=0
UPFZ=0
!底部挂架6个节点的反力数组：节点号，FX,FY,FZ
*DIM,DOWNSOL,ARRAY,6,4
DOWNFX=0
DOWNFY=0
DOWNFZ=0
X4 = 10000

!提取底部柱脚轴力
V1=0
V2=0
V3=0
V4=0

!定义单元类型、材料属性
ET,1,BEAM188
MP,EX,1,2.1E5
MP,PRXY,1,0.3
MP,DENS,1,7.85E-9

ET,2,LINK10
R,2,ALINK
MP,EX,2,2.1E6    !特别加强刚度
MP,DENS,2,7.85E-9
KEYOPT,2,3,1     !1表示仅受压，0表示仅受拉

!定义立柱截面，H型钢
SECTYPE,1,BEAM,I,,0
SECOFFSET,CENT
SECDATA,w1,w1,w2,t1,t1,t2
!定义腹杆截面，方管
!SECTYPE,2,BEAM,HREC,,0
!SECOFFSET,CENT
!SECDATA,b1,b1,t3,t3,t3,t3
!定义腹杆截面，角钢
SECTYPE,2,BEAM,L,,0
SECOFFSET,CENT
SECDATA,b1,b1,t3,t3
!定义顶部十字梁，实心矩形
SECTYPE,3,BEAM,RECT,,0
SECOFFSET,CENT
SECDATA,b2,b2
!外挂架主梁截面参数
SECTYPE,4,BEAM,HREC,,0
SECOFFSET,CENT
SECDATA,GW1,GH1,GT2,GT2,GT1,GT1
!外挂架竖向撑杆截面
SECTYPE,5,BEAM,CTUBE,,0
SECOFFSET,CENT
SECDATA,(GD1/2-GT3),GD1/2
!外挂架水平撑杆截面
SECTYPE,6,BEAM,CTUBE,,0
SECOFFSET,CENT
SECDATA,(GD2/2-GT4),GD2/2
!外挂架支撑框截面
SECTYPE,7,BEAM,I,,0
SECOFFSET,CENT
SECDATA,GW2,GW2,GH2,GT5,GT5,GT6

!定义节点
!塔身节点
*DO,i,1,(N2+1)
N,((i-1)*8+1),L1/2 ,L1/2 ,(i-1)*H1
N,((i-1)*8+2),L1/2 ,0    ,(i-1)*H1
N,((i-1)*8+3),L1/2 ,-L1/2,(i-1)*H1
N,((i-1)*8+4),0    ,-L1/2,(i-1)*H1
N,((i-1)*8+5),-L1/2,-L1/2,(i-1)*H1
N,((i-1)*8+6),-L1/2,0    ,(i-1)*H1
N,((i-1)*8+7),-L1/2,L1/2 ,(i-1)*H1
N,((i-1)*8+8),0    ,L1/2 ,(i-1)*H1
*ENDDO
N,((N2+1)*8+1),0,0,N2*H1  !顶部中心点
!定义上层夹持处8个LINK10仅受压单元的节点
!N3*8+1
N,((N2+1)*8+2),L1/2+LLINK ,L1/2       ,N3*H1  !X
N,((N2+1)*8+3),L1/2       ,L1/2+LLINK ,N3*H1  !Y
!N3*8+3
N,((N2+1)*8+4),L1/2+LLINK ,-L1/2      ,N3*H1  !X
N,((N2+1)*8+5),L1/2       ,-L1/2-LLINK,N3*H1  !Y
!N3*8+5
N,((N2+1)*8+6),-L1/2-LLINK,-L1/2      ,N3*H1  !X
N,((N2+1)*8+7),-L1/2      ,-L1/2-LLINK,N3*H1  !Y
!N3*8+7
N,((N2+1)*8+8),-L1/2-LLINK,L1/2       ,N3*H1  !X
N,((N2+1)*8+9),-L1/2      ,L1/2+LLINK ,N3*H1  !Y


!定义上层外挂架节点
N,((N2+1)*8+9+1),-G3-G1/2,G4,N3*H1
N,((N2+1)*8+9+2),-G1/2,G4,N3*H1
N,((N2+1)*8+9+3),G1/2,G4,N3*H1
N,((N2+1)*8+9+4),G2+G1/2,G4,N3*H1
N,((N2+1)*8+9+5),-G1/2,L1/2+LLINK,N3*H1
N,((N2+1)*8+9+6),-L1/2-LLINK,L1/2+LLINK,N3*H1
N,((N2+1)*8+9+7),L1/2+LLINK,L1/2+LLINK,N3*H1
N,((N2+1)*8+9+8),G1/2,L1/2+LLINK,N3*H1
N,((N2+1)*8+9+9),-G1/2,-L1/2-LLINK,N3*H1
N,((N2+1)*8+9+10),-L1/2-LLINK,-L1/2-LLINK,N3*H1
N,((N2+1)*8+9+11),L1/2+LLINK,-L1/2-LLINK,N3*H1
N,((N2+1)*8+9+12),G1/2,-L1/2-LLINK,N3*H1
N,((N2+1)*8+9+13),-G1/2,-L1/2-LLINK-200,N3*H1
N,((N2+1)*8+9+14),G1/2,-L1/2-LLINK-200,N3*H1
N,((N2+1)*8+9+15),-G1/2,G4,N3*H1-G5
N,((N2+1)*8+9+16),G1/2,G4,N3*H1-G5

!定义下层外挂架
N,((N2+1)*8+25+1),-G3-G1/2,G4,-LLINK
N,((N2+1)*8+25+2),-G1/2,G4,-LLINK
N,((N2+1)*8+25+3),G1/2,G4,-LLINK
N,((N2+1)*8+25+4),G2+G1/2,G4,-LLINK
N,((N2+1)*8+25+5),-G1/2,L1/2,-LLINK
N,((N2+1)*8+25+6),-L1/2,L1/2,-LLINK
N,((N2+1)*8+25+7),L1/2,L1/2,-LLINK
N,((N2+1)*8+25+8),G1/2,L1/2,-LLINK
N,((N2+1)*8+25+9),-G1/2,-L1/2,-LLINK
N,((N2+1)*8+25+10),-L1/2,-L1/2,-LLINK
N,((N2+1)*8+25+11),L1/2,-L1/2,-LLINK
N,((N2+1)*8+25+12),G1/2,-L1/2,-LLINK
N,((N2+1)*8+25+13),-G1/2,-L1/2-200,-LLINK
N,((N2+1)*8+25+14),G1/2,-L1/2-200,-LLINK
N,((N2+1)*8+25+15),-G1/2,G4,-LLINK-G5
N,((N2+1)*8+25+16),G1/2,G4,-LLINK-G5

!定义上层夹持中心节点，方便加载上部水平力
N,((N2+1)*8+42),0,0,N3*H1

!建立单元
TYPE,1
MAT,1
!1、立柱单元
SECNUM,1
*DO,i,1,N2
E,((i-1)*8+1),(i*8+1)
E,((i-1)*8+3),(i*8+3)
E,((i-1)*8+5),(i*8+5)
E,((i-1)*8+7),(i*8+7)
*ENDDO
!2、腹杆单元
SECNUM,2

*IF,TTYPE,EQ,1,THEN  !TTYPE=1四面人字腹杆
!2-1、横腹杆
*DO,i,1,(N2+1)
E,((i-1)*8+1),((i-1)*8+2)
E,((i-1)*8+2),((i-1)*8+3)
E,((i-1)*8+3),((i-1)*8+4)
E,((i-1)*8+4),((i-1)*8+5)
E,((i-1)*8+5),((i-1)*8+6)
E,((i-1)*8+6),((i-1)*8+7)
E,((i-1)*8+7),((i-1)*8+8)
E,((i-1)*8+8),((i-1)*8+1)
*ENDDO
!2-2、斜腹杆
*DO,i,1,N2
E,((i-1)*8+1),(i*8+2)
E,((i-1)*8+3),(i*8+2)
E,((i-1)*8+3),(i*8+4)
E,((i-1)*8+5),(i*8+4)
E,((i-1)*8+5),(i*8+6)
E,((i-1)*8+7),(i*8+6)
E,((i-1)*8+7),(i*8+8)
E,((i-1)*8+1),(i*8+8)
*ENDDO
*ELSEIF,TTYPE,EQ,2  !TTYPE=2四面K形腹杆
!2-1、横腹杆
*DO,i,1,(N2+1)
E,((i-1)*8+1),((i-1)*8+2)
E,((i-1)*8+2),((i-1)*8+3)
E,((i-1)*8+3),((i-1)*8+4)
E,((i-1)*8+4),((i-1)*8+5)
E,((i-1)*8+5),((i-1)*8+6)
E,((i-1)*8+6),((i-1)*8+7)
E,((i-1)*8+7),((i-1)*8+8)
E,((i-1)*8+8),((i-1)*8+1)
*ENDDO
!2-2、斜腹杆
*DO,i,1,N2-1,2
E,((i-1)*8+1),(i*8+3)
E,((i-1)*8+5),(i*8+3)
E,((i-1)*8+5),(i*8+7)
E,((i-1)*8+1),(i*8+7)
*ENDDO
*DO,i,2,N2,2
E,((i-1)*8+3),(i*8+1)
E,((i-1)*8+3),(i*8+5)
E,((i-1)*8+7),(i*8+5)
E,((i-1)*8+7),(i*8+1)
*ENDDO
*ELSE   !TTYPE=3人字+顺斜腹杆
!2-1、横腹杆
*DO,i,1,(N2+1)
E,((i-1)*8+1),((i-1)*8+2)
E,((i-1)*8+2),((i-1)*8+3)
E,((i-1)*8+3),((i-1)*8+4)
E,((i-1)*8+4),((i-1)*8+5)
E,((i-1)*8+5),((i-1)*8+6)
E,((i-1)*8+6),((i-1)*8+7)
E,((i-1)*8+7),((i-1)*8+8)
E,((i-1)*8+8),((i-1)*8+1)
*ENDDO
!2-2、斜腹杆
*DO,i,1,N2
E,((i-1)*8+1),(i*8+2)
E,((i-1)*8+3),(i*8+2)
E,((i-1)*8+5),(i*8+6)
E,((i-1)*8+7),(i*8+6)

E,((i-1)*8+1),(i*8+7)
E,((i-1)*8+5),(i*8+3)
*ENDDO
*ENDIF


!3、顶部十字单元
SECNUM,3
E,((N2+1)*8+1),N2*8+1
E,((N2+1)*8+1),N2*8+3
E,((N2+1)*8+1),N2*8+5
E,((N2+1)*8+1),N2*8+7

E,((N2+1)*8+42),N3*8+1
E,((N2+1)*8+42),N3*8+3
E,((N2+1)*8+42),N3*8+5
E,((N2+1)*8+42),N3*8+7

!4、上层夹持处LINK10单元
TYPE,2
MAT,2
REAL,2
E,N3*8+1,((N2+1)*8+2)
E,N3*8+1,((N2+1)*8+3)
E,N3*8+3,((N2+1)*8+4)
E,N3*8+3,((N2+1)*8+5)
E,N3*8+5,((N2+1)*8+6)
E,N3*8+5,((N2+1)*8+7)
E,N3*8+7,((N2+1)*8+8)
E,N3*8+7,((N2+1)*8+9)
!5、下层夹持处LINK10单元
E,1,((N2+1)*8+25+7)
E,3,((N2+1)*8+25+11)
E,5,((N2+1)*8+25+10)
E,7,((N2+1)*8+25+6)

!外挂架单元定义
TYPE,1
MAT,1
SECNUM,4  !外挂架主梁
E,((N2+1)*8+9+2),((N2+1)*8+9+5)
E,((N2+1)*8+9+5),((N2+1)*8+9+9)
E,((N2+1)*8+9+9),((N2+1)*8+9+13)
E,((N2+1)*8+9+3),((N2+1)*8+9+8)
E,((N2+1)*8+9+8),((N2+1)*8+9+12)
E,((N2+1)*8+9+12),((N2+1)*8+9+14)

E,((N2+1)*8+25+2),((N2+1)*8+25+5)
E,((N2+1)*8+25+5),((N2+1)*8+25+9)
E,((N2+1)*8+25+9),((N2+1)*8+25+13)
E,((N2+1)*8+25+3),((N2+1)*8+25+8)
E,((N2+1)*8+25+8),((N2+1)*8+25+12)
E,((N2+1)*8+25+12),((N2+1)*8+25+14)

SECNUM,5  !外挂架竖向撑杆
E,((N2+1)*8+9+9),((N2+1)*8+9+15)
E,((N2+1)*8+9+12),((N2+1)*8+9+16)

E,((N2+1)*8+25+9),((N2+1)*8+25+15)
E,((N2+1)*8+25+12),((N2+1)*8+25+16)

SECNUM,6  !外挂架水平撑杆
E,((N2+1)*8+9+1),((N2+1)*8+9+9)
E,((N2+1)*8+9+4),((N2+1)*8+9+12)

E,((N2+1)*8+25+1),((N2+1)*8+25+9)
E,((N2+1)*8+25+4),((N2+1)*8+25+12)

SECNUM,7  !外挂架支撑框
E,((N2+1)*8+9+5),((N2+1)*8+9+6)
E,((N2+1)*8+9+6),((N2+1)*8+9)
E,((N2+1)*8+9),((N2+1)*8+3)
E,((N2+1)*8+3),((N2+1)*8+9+7)
E,((N2+1)*8+9+7),((N2+1)*8+9+8)

E,((N2+1)*8+9+9),((N2+1)*8+9+10)
E,((N2+1)*8+9+10),((N2+1)*8+7)
E,((N2+1)*8+7),((N2+1)*8+5)
E,((N2+1)*8+5),((N2+1)*8+9+11)
E,((N2+1)*8+9+11),((N2+1)*8+9+12)

E,((N2+1)*8+9+6),((N2+1)*8+8)
E,((N2+1)*8+8),((N2+1)*8+6)
E,((N2+1)*8+6),((N2+1)*8+9+10)

E,((N2+1)*8+9+7),((N2+1)*8+2)
E,((N2+1)*8+2),((N2+1)*8+4)
E,((N2+1)*8+4),((N2+1)*8+9+11)

E,((N2+1)*8+25+5),((N2+1)*8+25+6)
E,((N2+1)*8+25+6),((N2+1)*8+25+7)
E,((N2+1)*8+25+7),((N2+1)*8+25+8)

E,((N2+1)*8+25+9),((N2+1)*8+25+10)
E,((N2+1)*8+25+10),((N2+1)*8+25+11)
E,((N2+1)*8+25+11),((N2+1)*8+25+12)

E,((N2+1)*8+25+6),((N2+1)*8+25+10)
E,((N2+1)*8+25+7),((N2+1)*8+25+11)




!定义边界条件
!外挂架节点约束
D,((N2+1)*8+9+1),UX,0
D,((N2+1)*8+9+1),UY,0
D,((N2+1)*8+9+1),UZ,0
D,((N2+1)*8+9+2),UX,0
D,((N2+1)*8+9+2),UY,0
D,((N2+1)*8+9+2),UZ,0
D,((N2+1)*8+9+3),UX,0
D,((N2+1)*8+9+3),UY,0
D,((N2+1)*8+9+3),UZ,0
D,((N2+1)*8+9+4),UX,0
D,((N2+1)*8+9+4),UY,0
D,((N2+1)*8+9+4),UZ,0
D,((N2+1)*8+9+15),UX,0
D,((N2+1)*8+9+15),UY,0
D,((N2+1)*8+9+15),UZ,0
D,((N2+1)*8+9+16),UX,0
D,((N2+1)*8+9+16),UY,0
D,((N2+1)*8+9+16),UZ,0

D,((N2+1)*8+25+1),UX,0
D,((N2+1)*8+25+1),UY,0
D,((N2+1)*8+25+1),UZ,0
D,((N2+1)*8+25+2),UX,0
D,((N2+1)*8+25+2),UY,0
D,((N2+1)*8+25+2),UZ,0
D,((N2+1)*8+25+3),UX,0
D,((N2+1)*8+25+3),UY,0
D,((N2+1)*8+25+3),UZ,0
D,((N2+1)*8+25+4),UX,0
D,((N2+1)*8+25+4),UY,0
D,((N2+1)*8+25+4),UZ,0
D,((N2+1)*8+25+15),UX,0
D,((N2+1)*8+25+15),UY,0
D,((N2+1)*8+25+15),UZ,0
D,((N2+1)*8+25+16),UX,0
D,((N2+1)*8+25+16),UY,0
D,((N2+1)*8+25+16),UZ,0

!定义底部节点耦合
CP,1,UX,1,((N2+1)*8+25+7)
CP,2,UY,1,((N2+1)*8+25+7)

CP,3,UX,3,((N2+1)*8+25+11)
CP,4,UY,3,((N2+1)*8+25+11)

CP,5,UX,5,((N2+1)*8+25+10)
CP,6,UY,5,((N2+1)*8+25+10)

CP,7,UX,7,((N2+1)*8+25+6)
CP,8,UY,7,((N2+1)*8+25+6)

!定义载荷
F,((N2+1)*8+1),FZ,-FFZ*10000
F,((N2+1)*8+42),FX,FFH*SIN(JJ)*10000
F,((N2+1)*8+42),FY,-FFH*COS(JJ)*10000
F,((N2+1)*8+1),MX,MMT*COS(JJ)*10000000   
F,((N2+1)*8+1),MY,MMT*SIN(JJ)*10000000

!求解
/SOLU
ACEL,,,9800  !重力加速度
ALLSEL,ALL
SOLVE

!后处理提取结果数据
/POST1
!提取上层挂架6个节点的反力
!主梁节点
UPSOL(1,1)=((N2+1)*8+9+2)
UPSOL(2,1)=((N2+1)*8+9+3)
!竖向撑杆节点
UPSOL(3,1)=((N2+1)*8+9+15)
UPSOL(4,1)=((N2+1)*8+9+16)
!水平撑杆节点
UPSOL(5,1)=((N2+1)*8+9+1)
UPSOL(6,1)=((N2+1)*8+9+4)

*DO,I,1,6
*GET,UPSOL(I,2),NODE,UPSOL(I,1),RF,FX
*GET,UPSOL(I,3),NODE,UPSOL(I,1),RF,FY
*GET,UPSOL(I,4),NODE,UPSOL(I,1),RF,FZ
*ENDDO
!附着位置合力
*DO,I,1,6
UPFX=UPFX+UPSOL(I,2)
*ENDDO
*DO,I,1,6
UPFY=UPFY+UPSOL(I,3)
*ENDDO
*DO,I,1,6
UPFZ=UPFZ+UPSOL(I,4)
*ENDDO


!提取下层挂架6个节点的反力

DOWNSOL(1,1)=((N2+1)*8+25+2)
DOWNSOL(2,1)=((N2+1)*8+25+3)

DOWNSOL(3,1)=((N2+1)*8+25+15)
DOWNSOL(4,1)=((N2+1)*8+25+16)

DOWNSOL(5,1)=((N2+1)*8+25+1)
DOWNSOL(6,1)=((N2+1)*8+25+4)


*DO,I,1,6
*GET,DOWNSOL(I,2),NODE,DOWNSOL(I,1),RF,FX
*GET,DOWNSOL(I,3),NODE,DOWNSOL(I,1),RF,FY
*GET,DOWNSOL(I,4),NODE,DOWNSOL(I,1),RF,FZ
*ENDDO
!底部位置三个方向合力
*DO,I,1,6
DOWNFX=DOWNFX+DOWNSOL(I,2)
*ENDDO
*DO,I,1,6
DOWNFY=DOWNFY+DOWNSOL(I,3)
*ENDDO
*DO,I,1,6
DOWNFZ=DOWNFZ+DOWNSOL(I,4)
*ENDDO

*IF,TTYPE,EQ,1,THEN  !TTYPE=1四面人字腹杆
*GET,V1,ELEM,(N2*20+24+1),SMISC,1
*GET,V2,ELEM,(N2*20+24+2),SMISC,1
*GET,V3,ELEM,(N2*20+24+3),SMISC,1
*GET,V4,ELEM,(N2*20+24+4),SMISC,1
*ELSEIF,TTYPE,EQ,2   !TTYPE=2四面K形腹杆
*GET,V1,ELEM,(N2*16+24+1),SMISC,1
*GET,V2,ELEM,(N2*16+24+2),SMISC,1
*GET,V3,ELEM,(N2*16+24+3),SMISC,1
*GET,V4,ELEM,(N2*16+24+4),SMISC,1
*ELSE   !TTYPE=3人字+顺斜腹杆
*GET,V1,ELEM,(N2*18+24+1),SMISC,1
*GET,V2,ELEM,(N2*18+24+2),SMISC,1
*GET,V3,ELEM,(N2*18+24+3),SMISC,1
*GET,V4,ELEM,(N2*18+24+4),SMISC,1
*ENDIF


!写出结果到文件
*CFOPEN,TTWG360,txt,,Append

*IF,JJ,EQ,0,THEN
!塔身总高
*VWRITE,N2*H1/1000,'m'
('塔身总长H=',F12.2,'|',A12)
!夹持距离
*VWRITE,N3*H1/1000,'m'
('夹持距离J=',F12.2,'|',A12)
!悬臂高度
*VWRITE,(N2-N3)*H1/1000,'m'
('悬臂高度C=',F12.2,'|',A12)
!标准节中心距
*VWRITE,L1,'mm'
('塔身中心距L1=',F12.2,'|',A12)
!顶部弯矩
*VWRITE,MMT,'t.m'
('顶部弯矩M=',F12.2,'|',A12)
!水平力
*VWRITE,FFH,'t'
('风载水平力F_H=',F12.2,'|',A12)
!顶部垂直力
*VWRITE,FFZ,'t'
('顶部垂直力F_V=',F12.2,'|',A12)
!标准节立柱截面尺寸
*VWRITE,w1,'x',w2,'x',t2,'x',t1
('立柱截面','H型钢',F4.0,A1,F4.0,A1,F4.0,A1,F4.0)
!腹杆截面尺寸
*VWRITE,b1,'x',t3
('腹杆截面','角钢',F4.0,A1,F4.0)
*ENDIF

*VWRITE,
('----------------------------------')
*VWRITE,FLAG
('求解结果-工况',F8.0)
*VWRITE,
('----------------------------------')

*VWRITE,
('-----上层外挂架节点反力-----')
*VWRITE,'Node','F_X(t)','F_Y(t)','F_Z(t)'
(A8,'|',A8,'|',A8,'|',A8)
*DO,J,1,6
*VWRITE,UPSOL(J,1)/1,UPSOL(J,2)/X4,UPSOL(J,3)/X4,UPSOL(J,4)/X4
(F8.0,'|',F8.1,'|',F8.1,'|',F8.1)
*ENDDO
*VWRITE,'Total',UPFX/X4,UPFY/X4,UPFZ/X4
(A8,'|',F8.1,'|',F8.1,'|',F8.1)
*VWRITE,
('----下层外挂架节点反力----')
*VWRITE,'Node','F_X(t)','F_Y(t)','F_Z(t)'
(A8,'|',A8,'|',A8,'|',A8)
*DO,J,1,6
*VWRITE,DOWNSOL(J,1)/1,DOWNSOL(J,2)/X4,DOWNSOL(J,3)/X4,DOWNSOL(J,4)/X4
(F8.0,'|',F8.1,'|',F8.1,'|',F8.1)
*ENDDO
*VWRITE,'Total',DOWNFX/X4,DOWNFY/X4,DOWNFZ/X4
(A8,'|',F8.1,'|',F8.1,'|',F8.1)
*VWRITE,
('-----塔身底部柱脚反力-----')
*VWRITE,'V1(t)','V2(t)','V3(t)','V4(t)'
(A8,'|',A8,'|',A8,'|',A8)
*VWRITE,V1/X4,V2/X4,V3/X4,V4/X4
(F8.1,'|',F8.1,'|',F8.1,'|',F8.1)
*VWRITE,
('---------------END----------------')
*CFCLOS
!写出到文件结束

*ENDDO     !循环求解结束