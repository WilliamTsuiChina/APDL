! 2021-02-05
! ANSYS计算宏（适用于附着节点反力计算）
! Author:徐明
! Mail:342668998@qq.com
! HomePage:https://github.com/WilliamTsuiChina
! 一点加载，6点反力，6杆轴力
! 注意：本版本参数定义均在文件内修改(在‘定义参数’区域内)，
! ANSYS内执行仅需要输入宏名称‘F1C6A6S360’。
! 使用前提：模型已经定义完成，划好网格，约束等边界条件定义完成。
! 坐标系约定: 从附着框看向建筑物的方向为Y+向，右手为X+向，竖直向上为Z+向。
!----------------------------------------------------------------------
!------------------------定义参数start----------------------------------
!----------------------------------------------------------------------
!加载关键点编号
a = 1
!垂直力，单位t（常规附着计算用不到）
f_v = 0
!水平力，单位t
f_h = 72
!弯矩，单位t.m（常规附着计算用不到）
f_m = 0
!扭矩，单位t.m
f_mk = 0
!支座节点编号，若不足6个，其余可重复补齐
node01 = 249
node02 = 122
node03 = 122
node04 = 122
node05 = 122
node06 = 122
!杆单元编号，若不足6个，其余可重复补齐
elem01 = 138
elem02 = 110
elem03 = 70
elem04 = 70
elem05 = 70
elem06 = 70
!----------------------------------------------------------------------
!------------------------定义参数end------------------------------------
!----------------------------------------------------------------------
!------------------------以下数据无需修改-------------------------------
f_v = f_v*10000
f_h = f_h*10000
f_m = f_m*10000000
f_mk = f_m*10000000
X4 = 10000
X7 = 10000000
*DIM,NONUM,ARRAY,6
*DIM,ELNUM,ARRAY,6
NONUM(1) = node01
NONUM(2) = node02
NONUM(3) = node03
NONUM(4) = node04
NONUM(5) = node05
NONUM(6) = node06
ELNUM(1) = elem01
ELNUM(2) = elem02
ELNUM(3) = elem03
ELNUM(4) = elem04
ELNUM(5) = elem05
ELNUM(6) = elem06
*DIM,FANLI,ARRAY,6,9
*DIM,ZHOULI,ARRAY,6,4
/PREP7
*AFUN,DEG
*CFOPEN,NodeRes,txt,,Append
*VWRITE,'Case','No','Node','F_X(t)','F_Y(t)','F_Z(t)','M_X(t.m)','M_Y(t.m)','M_Z(t.m)'
(A8,'|',A8,'|',A8,'|',A12,'|',A12,'|',A12,'|',A12,'|',A12,'|',A12)
*CFCLOS
*CFOPEN,ElemRes,txt,,Append
*VWRITE,'Case','No','Elem','F_A(t)'
(A8,'|',A8,'|',A8,'|',A12)
*CFCLOS
!----------------------------------------------------------------------
!------------------------循环求解start----------------------------------
!----------------------------------------------------------------------
*DO, JJ, 0, 359, 1
/SOLU
! Z向垂直力
FK,a,FZ,-f_v
! X向水平加载
FK,a,FX,f_h*SIN(JJ)
! Y向水平加载
FK,a,FY,-f_h*COS(JJ)
! 弯矩加载
FK,a,MX,f_m*COS(JJ)
FK,a,MY,f_m*SIN(JJ)
! 扭矩加载
FK,a,MZ,f_mk
SOLVE
/POST1
*DO,I,1,6
FANLI(I,1) = JJ + 1
FANLI(I,2) = I
FANLI(I,3) = NONUM(I)
*GET,FANLI(I,4),NODE,NONUM(I),RF,FX
*GET,FANLI(I,5),NODE,NONUM(I),RF,FY
*GET,FANLI(I,6),NODE,NONUM(I),RF,FZ
*GET,FANLI(I,7),NODE,NONUM(I),RF,MX
*GET,FANLI(I,8),NODE,NONUM(I),RF,MY
*GET,FANLI(I,9),NODE,NONUM(I),RF,MZ
*ENDDO
*CFOPEN,NodeRes,txt,,Append
*DO,J,1,6
*VWRITE,FANLI(J,1)/1,FANLI(J,2)/1,FANLI(J,3)/1,FANLI(J,4)/X4,FANLI(J,5)/X4,FANLI(J,6)/X4,FANLI(J,7)/X7,FANLI(J,8)/X7,FANLI(J,9)/X7
(F8.0,'|',F8.0,'|',F8.0,'|',F12.2,'|',F12.2,'|',F12.2,'|',F12.2,'|',F12.2,'|',F12.2)
*ENDDO
*CFCLOS
*DO,I,1,6
ZHOULI(I,1) = JJ + 1
ZHOULI(I,2) = I
ZHOULI(I,3) = ELNUM(I)
*GET,ZHOULI(I,4),ELEM,ELNUM(I),SMISC,1
*ENDDO
*CFOPEN,ElemRes,txt,,Append
*DO,J,1,6
*VWRITE,ZHOULI(J,1)/1,ZHOULI(J,2)/1,ZHOULI(J,3)/1,ZHOULI(J,4)/X4
(F8.0,'|',F8.0,'|',F8.0,'|',F12.2)
*ENDDO
*CFCLOS
*ENDDO
!----------------------------------------------------------------------
!------------------------循环求解end------------------------------------
!----------------------------------------------------------------------